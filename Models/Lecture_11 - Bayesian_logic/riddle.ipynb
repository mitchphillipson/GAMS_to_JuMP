{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 34,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP\n",
    "using Ipopt\n",
    "using GLPK\n",
    "using GamsStructure"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "This problem was passed to my by my colleague Phil Graves.  I also need to \n",
    "thank Erwin Kalvalagen for teaching me what is meant by a \"cut\" in integer\n",
    "programming.  \n",
    "\n",
    "Thomas Rutherford\n",
    "Economics Department\n",
    "University of Colorado\n",
    "\n",
    "GIVEN\n",
    "\n",
    "1. In a street there are five houses, painted five different colours. \n",
    "2. In each house lives a person of different nationality \n",
    "3. These five homeowners each drink a different kind of beverage, smoke \n",
    "different brand of cigar and keep a different pet. \n",
    "\n",
    "THE QUESTION: WHO OWNS THE FISH? \n",
    "\n",
    "HINTS \n",
    "\n",
    "1. The Brit lives in a red house. \n",
    "2. The Swede keeps dogs as pets. \n",
    "3. The Dane drinks tea. \n",
    "4. The Green house is just to the left of the White house. \n",
    "5. The owner of the Green house drinks coffee. \n",
    "6. The person who smokes Pall Mall rears birds. \n",
    "7. The owner of the Yellow house smokes Dunhill. \n",
    "8. The man living in the centre house drinks milk. \n",
    "9. The Norwegian lives in the first house. \n",
    "10. The man who smokes Blends lives next to the one who keeps cats. \n",
    "11. The man who keeps horses lives next to the man who smokes Dunhill. \n",
    "12. The man who smokes Blue Master drinks beer. \n",
    "13. The German smokes Prince. \n",
    "14. The Norwegian lives next to the blue house. \n",
    "15. The man who smokes Blends has a neighbour who drinks water. \n",
    "\n",
    "It has been asserted that Albert Einstein wrote this riddle early\n",
    "during the 19th century. He said that 98% of the world population\n",
    "would not be able to solve it. \n",
    "\n",
    "Erwin Kalvalagen pointed me to a web site which comments on Einstein\n",
    "being the author of this riddle (supposedly early on in his career):\n",
    "\n",
    "\"Finally, you may want to notice that the young Albert Einstein\n",
    "(1879-1955) could not possibly have authored the puzzle in this form:\n",
    "The Pall Mall brand of cigarettes was introduced by Butler & Butler in\n",
    "1899 (sold to American Tobacco in 1907 and Brown & Williamson in 1994)\n",
    "and Alfred Dunhill was established in 1893 (starting to manufacture\n",
    "pipes in 1907) when Einstein was still a young man. However, the Blue\n",
    "Master brand was introduced by J. L.  Tiedemann in 1937, when Einstein\n",
    "was 58!\""
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Define five sets defining the five characteristics of\n",
    "each individual.  The way the program is designed, the symbols\n",
    "used to define characteristics must be distinct (e.g., you\n",
    "cannot define an element of the smokes set S named \"Red\")."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "GU = GamsUniverse()\n",
    "\n",
    "@GamsSet(GU,:h,\"House\",begin\n",
    "    h1,\"\"\n",
    "    h2,\"\"\n",
    "    h3,\"\"\n",
    "    h4,\"\"\n",
    "    h5,\"\"\n",
    "end)\n",
    "\n",
    "@GamsSet(GU,:c,\"House Colors\",begin\n",
    "    red,\"\"\n",
    "    green,\"\"\n",
    "    yellow,\"\"\n",
    "    blue,\"\"\n",
    "    white,\"\"\n",
    "end)\n",
    "\n",
    "@GamsSet(GU,:s,\"Smokes\",begin\n",
    "    pall_mall,\"\"\n",
    "    dunhill,\"\"\n",
    "    blends,\"\"\n",
    "    prince,\"\"\n",
    "    blue_master,\"\"\n",
    "end)\n",
    "\n",
    "@GamsSet(GU,:b,\t\"beverages\",begin\n",
    "    Coffee, \"\"\n",
    "    Milk, \"\"\n",
    "    Beer, \"\"\n",
    "    Water, \"\"\n",
    "    Tea, \"\"\n",
    "end)\n",
    "\n",
    "@GamsSet(GU,:p,\"Pet\",begin\n",
    "    Dogs, \"\"\n",
    "    Birds, \"\"\n",
    "    Cats, \"\"\n",
    "    Horses, \"\"\n",
    "    Fish, \"\"\n",
    "end)\n",
    "\n",
    "@GamsSet(GU,:n, \"Nationality\",begin\n",
    "    Brit,\"\" \n",
    "    Swede, \"\"\n",
    "    Dane, \"\"\n",
    "    Norwegian, \"\"\n",
    "    German,\"\"\n",
    "end);"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "I'm using the GLPK solver since it supports binary variables, which this problem is all about. \n",
    "\n",
    "The idea here is to list every possible combination with a binary variable. Then use the constraints to fix many of these to be 0. The objective function is irrelevant, we are solely using the constraints to find where each entry is true."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 35,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Riddle (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "function Riddle(GU)\n",
    "\n",
    "    H = [h for h∈GU[:h]]\n",
    "    C = [c for c∈GU[:c]]\n",
    "    S = [s for s∈GU[:s]]\n",
    "    B = [b for b∈GU[:b]]\n",
    "    P = [p for p∈GU[:p]]\n",
    "    N = [n for n∈GU[:n]]\n",
    "\n",
    "    r = Model(GLPK.Optimizer)\n",
    "\n",
    "    @variable(r,Z[H,C,S,B,P,N],Bin) #Choice variable is 1 if we have h-c-s-b-p-n living on street\n",
    "\n",
    "    @objective(r,Min,0)\n",
    "\n",
    "    @constraints(r,begin\n",
    "        housing[h=H],   sum(Z[h,C,S,B,P,N])==1\n",
    "        colors[c=C],    sum(Z[H,c,S,B,P,N])==1\n",
    "        smokes[s=S],    sum(Z[H,C,s,B,P,N])==1\n",
    "        beverages[b=B], sum(Z[H,C,S,b,P,N])==1\n",
    "        pets[p=P],      sum(Z[H,C,S,B,p,N])==1\n",
    "        nations[n=N],   sum(Z[H,C,S,B,P,n])==1\n",
    "    end)\n",
    "\n",
    "    #4. The Green house is just to the left of the White house.\n",
    "    right_house = Dict(zip(H[1:end-1],H[2:end]))\n",
    "    left_house = Dict(zip(H[2:end],H[1:end-1]))\n",
    "\n",
    "    @constraint(r, hint4[h=H[1:end-1]],\n",
    "        sum(Z[h,[:green],S,B,P,N]) <= sum(Z[right_house[h],[:white],S,B,P,N])\n",
    "    )\n",
    "\n",
    "    #10. The man who smokes Blends lives next to the one who keeps cats.\n",
    "    @constraint(r, hint10[h=H[2:end-1]],\n",
    "        sum(Z[h,C,[:blends],B,P,N]) <=\n",
    "        sum(Z[right_house[h],C,S,B,[:Cats],N]) +sum(Z[left_house[h],C,S,B,[:Cats],N]) \n",
    "    )\n",
    "\n",
    "    #11. The man who keeps horses lives next to the man who smokes Dunhill.\n",
    "    @constraint(r, hint11[h=H[2:end-1]],\n",
    "        sum(Z[h,C,S,B,[:Horses],N]) <=\n",
    "        sum(Z[right_house[h],C,[:dunhill],B,P,N]) +sum(Z[left_house[h],C,[:dunhill],B,P,N]) \n",
    "    )  \n",
    "\n",
    "    #14. The Norwegian lives next to the blue house.\n",
    "    @constraint(r, hint14[h=H[2:end-1]],\n",
    "        sum(Z[h,C,S,B,P,[:Norwegian]]) <=\n",
    "        sum(Z[right_house[h],[:blue],S,B,P,N]) +sum(Z[left_house[h],[:blue],S,B,P,N]) \n",
    "    )     \n",
    "    \n",
    "\n",
    "    #15. The man who smokes Blends has a neighbour who drinks water. \n",
    "    @constraint(r, hint15[h=H[2:end-1]],\n",
    "        sum(Z[h,C,[:blends],B,P,N]) <=\n",
    "        sum(Z[right_house[h],C,S,[:Water],P,N]) +sum(Z[left_house[h],C,S,[:Water],P,N]) \n",
    "    )    \n",
    "\n",
    "    for h∈H,c∈C,s∈S,b∈B,p∈P,n∈N\n",
    "        #hint 1: The Brit lives in a red house. \n",
    "        if (c==:red) ⊻ (n == :Brit)\n",
    "            fix(Z[h,c,s,b,p,n],0,force=true)\n",
    "        end\n",
    "\n",
    "        #2. The Swede keeps Dogs as pets. \n",
    "        if (n == :Swede) ⊻ (p==:Dogs)\n",
    "            fix(Z[h,c,s,b,p,n],0,force=true)\n",
    "        end\n",
    "\n",
    "        #3. The Dane drinks tea.\n",
    "        if (n==:Dane) ⊻ (b ==:Tea)\n",
    "            fix(Z[h,c,s,b,p,n],0,force=true)\n",
    "        end\n",
    "\n",
    "        #5. The owner of the Green house drinks coffee. \n",
    "        if (c==:green) ⊻ (b==:Coffee)\n",
    "            fix(Z[h,c,s,b,p,n],0,force=true)\n",
    "        end\n",
    "\n",
    "        #6. The person who smokes Pall Mall rears Birds. \n",
    "        if (s == :pall_mall) ⊻ (p==:Birds)\n",
    "            fix(Z[h,c,s,b,p,n],0,force=true)\n",
    "        end\n",
    "\n",
    "        #7. The owner of the Yellow house smokes Dunhill. \n",
    "        if (c == :yellow) ⊻ (s==:dunhill)\n",
    "            fix(Z[h,c,s,b,p,n],0,force=true)\n",
    "        end\n",
    "\n",
    "        #8. The man living in the centre house drinks milk. \n",
    "        if (h==:h3) ⊻ (b==:Milk)\n",
    "            fix(Z[h,c,s,b,p,n],0,force=true)\n",
    "        end\n",
    "\n",
    "        #9. The Norwegian lives in the first house. \n",
    "        if (h==:h1) ⊻ (n==:Norwegian)\n",
    "            fix(Z[h,c,s,b,p,n],0,force=true)\n",
    "        end\n",
    "\n",
    "        #12. The man who smokes Blue Master drinks beer. \n",
    "        if (s==:blue_master) ⊻ (b==:Beer)\n",
    "            fix(Z[h,c,s,b,p,n],0,force=true)\n",
    "        end\n",
    "\n",
    "        #13. The German smokes Prince. \n",
    "        if (n==:German) ⊻ (s==:prince)\n",
    "            fix(Z[h,c,s,b,p,n],0,force=true)\n",
    "        end\n",
    "    end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "    return r\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 39,
   "metadata": {},
   "outputs": [],
   "source": [
    "riddle = Riddle(GU)\n",
    "set_silent(riddle)\n",
    "optimize!(riddle)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 40,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "h1, blue, blends, Water, Horses, Norwegian\n",
      "h2, yellow, dunhill, Tea, Cats, Dane\n",
      "h3, red, pall_mall, Milk, Birds, Brit\n",
      "h4, green, prince, Coffee, Fish, German\n",
      "h5, white, blue_master, Beer, Dogs, Swede\n"
     ]
    }
   ],
   "source": [
    "X = value.(riddle[:Z])\n",
    "\n",
    "for h∈GU[:h],c∈GU[:c],s∈GU[:s],b∈GU[:b],p∈GU[:p],n∈GU[:n]\n",
    "    if X[h,c,s,b,p,n] !=0\n",
    "        println(\"$h, $c, $s, $b, $p, $n\")\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.0",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.0"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
